;----------
; Includes

	!include "MUI2.nsh"
	!include "logiclib.nsh"
	!include "FileFunc.nsh"

;----------
; Custom defines

	!define NAME "@NAME@"
	!define EXEC_NAME "@EXEC_NAME@"
	!define APPFILE "${EXEC_NAME}.exe"
	!define VERSION "@VERSION@"
	!define SLUG "${NAME} v${VERSION}"
	!define UNINSTALL_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}"

;----------
; General

	Name "${Name}"
	BrandingText "${SLUG}"
	OutFile "${NAME} Setup.exe"
	InstallDir "$PROGRAMFILES\${NAME}"
	InstallDirRegKey HKCU "Software\${NAME}" ""
	RequestExecutionLevel admin

;----------
; UI

	!define MUI_ICON "${EXEC_NAME}.ico"
	!define MUI_UNICON "${EXEC_NAME}-uninstall.ico"
	!define MUI_ABORTWARNING
	!define MUI_WELCOMEPAGE_TITLE "${SLUG} Setup"
	!define MUI_WELCOMEFINISHPAGE_BITMAP "${EXEC_NAME}-side.bmp"
	!define MUI_HEADERIMAGE_BITMAP "${EXEC_NAME}-header.bmp"
	!define MUI_HEADERIMAGE "${EXEC_NAME}.bmp"
	!define MUI_HEADERIMAGE_BITMAP_NOSTRETCH

;----------
; Pages

	; Installer pages
	!insertmacro MUI_PAGE_WELCOME
	!insertmacro MUI_PAGE_COMPONENTS
	!insertmacro MUI_PAGE_DIRECTORY
	!insertmacro MUI_PAGE_INSTFILES
	!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\${APPFILE}"
	!define MUI_FINISHPAGE_RUN_NOTCHECKED
	!define MUI_FINISHPAGE_LINK "Visit ${NAME}'s Website"
	!define MUI_FINISHPAGE_LINK_LOCATION "https://tuba.geopjr.dev/"
	!insertmacro MUI_PAGE_FINISH

	; Uninstaller pages
	!insertmacro MUI_UNPAGE_CONFIRM
	!insertmacro MUI_UNPAGE_INSTFILES

;----------
; Language

	!insertmacro MUI_LANGUAGE "English"

;----------
; Section - Install App

	Section "-hidden app"
		SectionIn RO
		SetOutPath "$INSTDIR\bin"
		File /a /r "tuba_windows_portable\bin\"
		SetOutPath "$INSTDIR\lib"
		File /a /r "tuba_windows_portable\lib\"
		SetOutPath "$INSTDIR\share"
		File /a /r "tuba_windows_portable\share\"
		SetOutPath "$INSTDIR\"
		File "${EXEC_NAME}.ico"
		WriteRegStr HKCU "Software\${NAME}" "" $INSTDIR

		DeleteRegKey HKLM "${UNINSTALL_KEY}"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "DisplayName" "${NAME}"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "UninstallString" "$INSTDIR\uninstall.exe"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "InstallLocation" "$INSTDIR"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "Publisher" "GeopJr"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "DisplayIcon" "$INSTDIR\${EXEC_NAME}.ico"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "URLInfoAbout" "https://tuba.geopjr.dev/"
		WriteRegStr HKLM "${UNINSTALL_KEY}" "DisplayVersion" "${VERSION}"
		WriteRegDWORD HKLM "${UNINSTALL_KEY}" "NoModify" 1
		WriteRegDWORD HKLM "${UNINSTALL_KEY}" "NoRepair" 1

		${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
		IntFmt $0 "0x%08X" $0
		WriteRegDWORD HKLM "${UNINSTALL_KEY}" "EstimatedSize" "$0"

		WriteUninstaller "$INSTDIR\uninstall.exe"
	SectionEnd

;----------
; Section - Shortcut

	Section "Desktop Shortcut" DeskShort
		CreateShortCut "$DESKTOP\${NAME}.lnk" "$INSTDIR\bin\${APPFILE}"
		CreateShortCut "$SMPrograms\${NAME}.lnk" "$INSTDIR\bin\${APPFILE}"
	SectionEnd

;----------
; Remove empty parent directories

	Function un.RMDirUP
		!define RMDirUP '!insertmacro RMDirUPCall'

		!macro RMDirUPCall _PATH
			push '${_PATH}'
			Call un.RMDirUP
		!macroend

		; $0 - current folder
		ClearErrors

		Exch $0
		; DetailPrint "ASDF - $0\.."
		RMDir "$0\.."

		IfErrors Skip
		${RMDirUP} "$0\.."
		Skip:
			Pop $0
	FunctionEnd

;----------
; Section - Uninstaller

Section "Uninstall"
	; Delete Shortcut
	Delete "$DESKTOP\${NAME}.lnk"
	Delete "$SMPrograms\${NAME}.lnk"

	; Delete Uninstall
	Delete "$INSTDIR\uninstall.exe"

	; Delete Folder
	RMDir /r "$INSTDIR"
	${RMDirUP} "$INSTDIR"

	DeleteRegKey /ifempty HKCU "Software\${NAME}"
	DeleteRegKey HKLM "${UNINSTALL_KEY}"
SectionEnd
